
timestamps
{
	def jdk = 'openjdk-8-deb9'
	def isRelease = env.BRANCH_NAME.toString().equals("master")

	properties([
			buildDiscarder(logRotator(
					numToKeepStr : isRelease ? '1000' : '30'
			))
	])

	env.DATABASE_HOST = 'postgresql96.ci.test'

	//noinspection GroovyAssignabilityCheck
	node('GitCloneExedio && ' + jdk + ' && ' + env.DATABASE_HOST)
	{
		try
		{
			abortable
			{
				echo("Delete working dir before build")
				deleteDir()

				def scmResult = checkout scm
				computeGitTree(scmResult)

				env.BUILD_TIMESTAMP = new Date().format("yyyy-MM-dd_HH-mm-ss")
				env.JAVA_HOME = tool type: 'jdk', name: jdk
				env.PATH = "${env.JAVA_HOME}/bin:${env.PATH}"
				env.SCHEMA = "ci_${HOSTNAME}_${EXECUTOR_NUMBER}"

				sh 'echo' +
						' HOSTNAME -${HOSTNAME}-' +
						' EXECUTOR_NUMBER -${EXECUTOR_NUMBER}-' +
						' scmResult=' + scmResult +
						' BUILD_TIMESTAMP -${BUILD_TIMESTAMP}-' +
						' BRANCH_NAME -${BRANCH_NAME}-' +
						' BUILD_NUMBER -${BUILD_NUMBER}-' +
						' BUILD_ID -${BUILD_ID}-' +
						' SCHEMA=' + env.SCHEMA

				sh 'rm -f conf/environment/*.properties'

				writeEnv(
						'pg96',
						''
				)
				writeEnv(
						'pg96-mysql',
						'disableSupport.semicolon=true\n' +
						'disableSupport.nativeDate=true\n'
				)
				writeEnv(
						'pg96-nprep',
						'disableSupport.preparedStatements=true\n'
				)
				writeEnv(
						'pg96-nstmp',
						'cache.stamps=false\n'
				)
				writeEnv(
						'pg96-public',
						'dialect.connection.schema=public\n' +
						'schema.primaryKeyGenerator=sequence\n'
				)
				writeEnv(
						'pg96-sq',
						'schema.primaryKeyGenerator=sequence\n' +
						'dialect.pgcryptoSchema=<disabled>\n'
				)

				sh 'ant/bin/ant -noinput' +
						' -f macros/resetSchema.xml' +
						' postgresql' +
						' -Dfile=conf/environment/pg96.properties'

				sh "ant/bin/ant -noinput clean testWithEnv" +
						' "-Dbuild.tag=git ${BRANCH_NAME} ' + scmResult.GIT_COMMIT + ' ' + scmResult.GIT_TREE + ' jenkins ${BUILD_NUMBER} ${BUILD_TIMESTAMP}"' +
						' -Dskip.instrument=true' + // already verified in Jenkinsfile
						' -Ddisable-ansi-colors=true'
			}
		}
		catch(Exception e)
		{
			//todo handle script returned exit code 143
			throw e
		}
		finally
		{
			// because junit failure aborts ant
			junit(
					allowEmptyResults: false,
					testResults: 'build/testresults/**/*.xml',
			)
			archiveArtifacts(
					'build/testprotocol.*,' +
					'build/classes/runtime/src/com/exedio/cope/testprotocol.properties,' +
					'build/*.log,' +
					'build/testtmpdir'
			)
			def to = emailextrecipients([culprits(), requestor()])
			//TODO details
			step([$class: 'Mailer',
					recipients: to,
					attachLog: true,
					notifyEveryUnstableBuild: true])

			echo("Delete working dir after build")
			deleteDir()
		}
	}
}

def abortable(Closure body)
{
	try
	{
		body.call()
	}
	catch(hudson.AbortException e)
	{
		if(e.getMessage().contains("exit code 143"))
			return
		throw e
	}
}

def computeGitTree(scmResult)
{
	sh "git cat-file -p " + scmResult.GIT_COMMIT + " | grep '^tree ' | sed -e 's/^tree //' > .git/jenkins-head-tree"
	scmResult.GIT_TREE = readFile('.git/jenkins-head-tree').trim()
}

def writeEnv(name, text)
{
	withCredentials([string(credentialsId: env.DATABASE_HOST, variable: 'PASSWORD')])
	{
		writeFile(
			file: 'conf/environment/' + name + '.properties',
			text:
					'connection.url=jdbc:postgresql://' + env.DATABASE_HOST + '/' + env.SCHEMA + '\n' +
					'connection.username=' + env.SCHEMA + '\n' +
					'connection.password=' + PASSWORD + '\n' +
					'x-build.schema=' + env.SCHEMA + '\n' +
					'x-build.dialect=postgresql\n' +
					'x-build.driver=postgresql-bundle.jar\n' +
					text
		)
	}
}
