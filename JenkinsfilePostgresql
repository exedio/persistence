
timestamps
{
	def jdk = 'openjdk-11'
	def isRelease = env.BRANCH_NAME.toString().equals("master")

	properties([
			gitLabConnection(env.GITLAB_CONNECTION),
			buildDiscarder(logRotator(
					numToKeepStr : isRelease ? '50' : '30'
			))
	])

	def database = '11.12'

	//noinspection GroovyAssignabilityCheck
	node('GitCloneExedio && docker')
	{
		try
		{
			abortable
			{
				echo("Delete working dir before build")
				deleteDir()

				def buildTag = makeBuildTag(checkout(scm))
				updateGitlabCommitStatus name: 'Postgresql', state: 'running'

				sh 'rm -f conf/environment/*.properties'

				writeEnv(
						'pg11',
						''
				)
				writeEnv(
						'pg11-mysql',
						'disableSupport.nativeDate=true\n'
				)
				writeEnv(
						'pg11-nprep',
						'disableSupport.preparedStatements=true\n'
				)
				writeEnv(
						'pg11-nstmp',
						'cache.stamps=false\n'
				)
				writeEnv(
						'pg11-public',
						'dialect.connection.schema=public\n' +
						'schema.primaryKeyGenerator=sequence\n'
				)
				writeEnv(
						'pg11-sq',
						'schema.primaryKeyGenerator=sequence\n' +
						'dialect.pgcryptoSchema=<disabled>\n'
				)
				writeEnv(
						'pg11-vault',
						'vault=true\n' +
						'vault.service=com.exedio.cope.vaultmock.VaultMockService\n' +
						'vault.isAppliedToAllFields=true\n' +
						'schema.primaryKeyGenerator=sequence\n'
				)

				def dockerName = env.JOB_NAME.replace("/", "-") + "-" + env.BUILD_NUMBER
				def dockerDate = new Date().format("yyyyMMdd")
				def mainImage = docker.build(
						'exedio-jenkins:' + dockerName + '-' + dockerDate,
						'--build-arg JDK=' + jdk + ' ' +
						'conf/main')
				def dbImage = docker.build(
						'exedio-jenkins:' + dockerName + '-' + dockerDate + '-db',
						'--build-arg VERSION=' + database + ' ' +
						'conf/postgresql')

				def dbBridge = sh ( script:
						"docker network create " +
								dockerName + "-db " +
								"--driver bridge " +
								"--internal",
						returnStdout: true).trim()
				try
				{
					dbImage.withRun(
							"--name '" + dockerName + "-db' " +
							"--cap-drop all " +
							"--cap-add CHOWN " +
							"--cap-add SETGID " +
							"--cap-add SETUID " +
							"--security-opt no-new-privileges " +
							"--tmpfs /var/lib/postgresql/data:rw " +
							"--network " + dbBridge + " " +
							"--network-alias=test_db_host " +
							"--dns-opt timeout:1 --dns-opt attempts:1") // fail faster
					{ c ->
						mainImage.inside(
								"--name '" + dockerName + "' " +
								"--cap-drop all " +
								"--security-opt no-new-privileges " +
								"--hostname mydockerhostname " +
								"--network " + dbBridge + " " +
								"--dns-opt timeout:1 --dns-opt attempts:1") // fail faster
						{
							sh "ant/bin/ant -noinput clean testWithEnv" +
									' "-Dbuild.tag=' + buildTag + '"' +
									' -Dskip.instrument=true' + // already verified in Jenkinsfile
									' -Druntime.test.withEnv.setup.postgresql.url=jdbc:postgresql://test_db_host/' +
									' -Ddisable-ansi-colors=true'
						}
						sh "docker logs " + c.id + " &> db.log"
						archiveArtifacts 'db.log'
					}
				}
				finally
				{
					sh "docker network rm " + dbBridge
				}
			}
		}
		catch(Exception e)
		{
			//todo handle script returned exit code 143
			throw e
		}
		finally
		{
			// because junit failure aborts ant
			junit(
					allowEmptyResults: false,
					testResults: 'build/testresults/**/*.xml',
			)
			archiveArtifacts(
					'build/testprotocol.*,' +
					'build/classes/runtime/src/com/exedio/cope/testprotocol.properties,' +
					'build/*.log,' +
					'build/testtmpdir'
			)
			def to = emailextrecipients([culprits(), requestor()])
			//TODO details
			step([$class: 'Mailer',
					recipients: to,
					attachLog: true,
					notifyEveryUnstableBuild: true])

			updateGitlabCommitStatus name: 'Postgresql', state: currentBuild.resultIsBetterOrEqualTo("SUCCESS") ? "success" : "failed" // https://docs.gitlab.com/ee/api/commits.html#post-the-build-status-to-a-commit
			echo("Delete working dir after build")
			deleteDir()
		}
	}
}

def abortable(Closure body)
{
	try
	{
		body.call()
	}
	catch(hudson.AbortException e)
	{
		if(e.getMessage().contains("exit code 143"))
			return
		throw e
	}
}

def makeBuildTag(scmResult)
{
	return 'build ' +
			env.BRANCH_NAME + ' ' +
			env.BUILD_NUMBER + ' ' +
			new Date().format("yyyy-MM-dd") + ' ' +
			scmResult.GIT_COMMIT + ' ' +
			sh (script: "git cat-file -p " + scmResult.GIT_COMMIT + " | grep '^tree ' | sed -e 's/^tree //'", returnStdout: true).trim()
}

def writeEnv(name, text)
{
	writeFile(
			file: 'conf/environment/' + name + '.properties',
			text:
					'connection.url=jdbc:postgresql://test_db_host/test_db_database\n' +
					'connection.username=test_db_user\n' +
					'connection.password=test_db_password\n' +
					'dialect.connection.schema=test_db_schema\n' +
					'dialect.pgcryptoSchema=test_db_pgcrypto_schema\n' +
					'x-build.dialect=postgresql\n' +
					'x-build.driver=postgresql.jar\n' +
					text
		)
}
