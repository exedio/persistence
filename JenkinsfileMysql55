
timestamps
{
	def jdk = 'openjdk-8-deb9'
	def isRelease = env.BRANCH_NAME.toString().equals("master")

	properties([
			buildDiscarder(logRotator(
					numToKeepStr : isRelease ? '1000' : '30'
			))
	])

	env.DATABASE_HOST = 'mysql55.ci.test'

	//noinspection GroovyAssignabilityCheck
	node('GitCloneExedio && ' + jdk + ' && ' + env.DATABASE_HOST)
	{
		try
		{
			abortable
			{
				echo("Delete working dir before build")
				deleteDir()

				def buildTag = makeBuildTag(checkout(scm))

				env.JAVA_HOME = tool type: 'jdk', name: jdk
				env.PATH = "${env.JAVA_HOME}/bin:${env.PATH}"
				env.SCHEMA = "ci_${HOSTNAME}_${EXECUTOR_NUMBER}"

				sh 'rm -f conf/environment/*.properties'

				writeEnv(
						'my55',
						''
				)
				writeEnv(
						'my55-legacy',
						'disableSupport.semicolon=true\n' +
						'dialect.fullSequenceColumnName=false\n' +
						'dialect.longConstraintNames=false\n' +
						'dialect.smallIntegerTypes=false\n' +
						'dialect.utf8mb4=false\n'
				)
				writeEnv(
						'my55-nprep',
						'disableSupport.preparedStatements=true\n'
				)
				writeEnv(
						'my55-nprep-legacy',
						'dialect.fullSequenceColumnName=false\n' +
						'dialect.longConstraintNames=false\n' +
						'dialect.smallIntegerTypes=false\n' +
						'dialect.utf8mb4=false\n' +
						'disableSupport.preparedStatements=true\n'
				)
				writeEnv(
						'my55-nstmp',
						'cache.stamps=false\n'
				)
				writeEnv(
						'my55-nstmp-sq',
						'cache.stamps=false\n' +
						'schema.primaryKeyGenerator=sequence\n'
				)
				writeEnv(
						'my55-sq',
						'schema.primaryKeyGenerator=sequence\n'
				)
				writeEnv(
						'my55-sqb',
						'schema.primaryKeyGenerator=batchedSequence\n'
				)
				writeEnv(
						'my55-unique',
						'disableSupport.uniqueViolation=true\n'
				)

				sh 'ant/bin/ant -noinput' +
						' -f macros/resetSchema.xml' +
						' mysql' +
						' -Dfile=conf/environment/my55.properties'

				sh "ant/bin/ant -noinput clean testWithEnv" +
						' "-Dbuild.tag=' + buildTag + '"' +
						' -Dskip.instrument=true' + // already verified in Jenkinsfile
						' -Ddisable-ansi-colors=true'
			}
		}
		catch(Exception e)
		{
			//todo handle script returned exit code 143
			throw e
		}
		finally
		{
			// because junit failure aborts ant
			junit(
					allowEmptyResults: false,
					testResults: 'build/testresults/**/*.xml',
			)
			archiveArtifacts(
					'build/testprotocol.*,' +
					'build/classes/runtime/src/com/exedio/cope/testprotocol.properties,' +
					'build/*.log,' +
					'build/testtmpdir'
			)
			def to = emailextrecipients([culprits(), requestor()])
			//TODO details
			step([$class: 'Mailer',
					recipients: to,
					attachLog: true,
					notifyEveryUnstableBuild: true])

			echo("Delete working dir after build")
			deleteDir()
		}
	}
}

def abortable(Closure body)
{
	try
	{
		body.call()
	}
	catch(hudson.AbortException e)
	{
		if(e.getMessage().contains("exit code 143"))
			return
		throw e
	}
}

def makeBuildTag(scmResult)
{
	return 'build ' +
			env.BRANCH_NAME + ' ' +
			env.BUILD_NUMBER + ' ' +
			new Date().format("yyyy-MM-dd") + ' ' +
			scmResult.GIT_COMMIT + ' ' +
			sh (script: "git cat-file -p " + scmResult.GIT_COMMIT + " | grep '^tree ' | sed -e 's/^tree //'", returnStdout: true).trim()
}

def writeEnv(name, text)
{
	withCredentials([string(credentialsId: env.DATABASE_HOST, variable: 'PASSWORD')])
	{
		writeFile(
			file: 'conf/environment/' + name + '.properties',
			text:
					'connection.url=jdbc:mysql://' + env.DATABASE_HOST + '/' + env.SCHEMA + '\n' +
					'connection.username=' + env.SCHEMA + '\n' +
					'connection.password=' + PASSWORD + '\n' +
					'x-build.schemasavepoint=' +
							/FAILS: SHOW MASTER STATUS returns empty result, probably because binlog is disabled/ + '\n' +
					'x-build.dialect=mysql\n' +
					'x-build.driver=mysql-connector-java.jar\n' +
					text
		)
	}
}
