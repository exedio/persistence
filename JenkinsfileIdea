
timestamps
{
	def jdk = 'openjdk-8'
	def isRelease = env.BRANCH_NAME.toString().equals("master")

	properties([
			buildDiscarder(logRotator(
					numToKeepStr         : isRelease ? '1000' : '30',
					artifactNumToKeepStr : isRelease ?  '100' :  '2'
			))
	])

	def idea = '2020.3.1'
	def ideaSHA256 = '06abca33b240b24f447dada437f5ce7387b47644c76378230254d6163882a42a'

	//noinspection GroovyAssignabilityCheck
	node('GitCloneExedio && docker')
	{
		try
		{
			abortable
			{
				echo("Delete working dir before build")
				deleteDir()

				checkout scm

				recordIssues(
						failOnError: true,
						enabledForFailure: true,
						ignoreFailedBuilds: false,
						qualityGates: [[threshold: 1, type: 'TOTAL_HIGH', unstable: true]],
						tools: [
								taskScanner(
										excludePattern:
												'JenkinsfileIdea,' + // TODO do not exclude the whole JenkinsfileIdea, but just the FIXME line
												'.git/**,lib/**,ant/**,' +
												'**/*.jar,**/*.zip,**/*.tgz,**/*.jpg,**/*.gif,**/*.png,**/*.tif,**/*.webp,**/*.pdf,**/*.eot,**/*.ttf,**/*.woff,**/*.woff2,**/keystore', // binary file types
										highTags: 'FIXME', // causes build to become unstable
										normalTags: 'TODO', // does not cause build to become unstable
										ignoreCase: true),
						],
				)

				def dockerName = env.JOB_NAME.replace("/", "-") + "-" + env.BUILD_NUMBER
				docker.
					build(
						'exedio-jenkins:' + dockerName + '-' + new Date().format("yyyyMMdd"),
						'--build-arg JDK=' + jdk + ' ' +
						'--build-arg IDEA=' + idea + ' ' +
						'--build-arg IDEA_SHA256=' + ideaSHA256 + ' ' +
						'conf/idea').
					inside(
						"--name '" + dockerName + "' " +
						"--cap-drop all " +
						"--security-opt no-new-privileges " +
						"--network none")
					{
						sh "/opt/idea/bin/inspect.sh " + env.WORKSPACE + " 'Project Default' idea-inspection-output"
					}
				archiveArtifacts 'idea-inspection-output/**'
				sh "rm idea-inspection-output/SpellCheckingInspection.xml"
				// replace project dir to prevent UnsupportedOperationException - will not be exposed in artifacts
				sh "find idea-inspection-output -name '*.xml' | xargs --no-run-if-empty sed --in-place -- 's=\\\$PROJECT_DIR\\\$="+env.WORKSPACE+"=g'"
				recordIssues(
						failOnError: true,
						enabledForFailure: true,
						ignoreFailedBuilds: false,
						qualityGates: [[threshold: 1, type: 'TOTAL', unstable: true]],
						tools: [
							ideaInspection(pattern: 'idea-inspection-output/**'),
						],
				)
			}
		}
		catch(Exception e)
		{
			//todo handle script returned exit code 143
			throw e
		}
		finally
		{
			def to = emailextrecipients([culprits(), requestor()])
			//TODO details
			step([$class: 'Mailer',
					recipients: to,
					attachLog: true,
					notifyEveryUnstableBuild: true])

			echo("Delete working dir after build")
			deleteDir()
		}
	}
}

def abortable(Closure body)
{
	try
	{
		body.call()
	}
	catch(hudson.AbortException e)
	{
		if(e.getMessage().contains("exit code 143"))
			return
		throw e
	}
}
