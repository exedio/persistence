
timestamps
{
	def jdk = 'openjdk-8'
	def isRelease = env.BRANCH_NAME.toString().equals("master")

	properties([
			buildDiscarder(logRotator(
					numToKeepStr : isRelease ? '1000' : '30'
			))
	])

	def database = '5.6.33'

	//noinspection GroovyAssignabilityCheck
	node('GitCloneExedio && docker')
	{
		try
		{
			abortable
			{
				echo("Delete working dir before build")
				deleteDir()

				def buildTag = makeBuildTag(checkout(scm))

				sh 'rm -f conf/environment/*.properties'

				writeEnv(
						'my56',
						''
				)
				writeEnv(
						'my56-legacy',
						'disableSupport.semicolon=true\n' +
						'dialect.fullSequenceColumnName=false\n' +
						'dialect.longConstraintNames=false\n' +
						'dialect.smallIntegerTypes=false\n' +
						'dialect.utf8mb4=false\n' +
						'disableSupport.nativeDate=true\n'
				)
				writeEnv(
						'my56-nprep',
						'disableSupport.preparedStatements=true\n'
				)
				writeEnv(
						'my56-nprep-legacy',
						'dialect.fullSequenceColumnName=false\n' +
						'dialect.longConstraintNames=false\n' +
						'dialect.smallIntegerTypes=false\n' +
						'dialect.utf8mb4=false\n' +
						'disableSupport.preparedStatements=true\n'
				)
				writeEnv(
						'my56-nstmp',
						'cache.stamps=false\n'
				)
				writeEnv(
						'my56-nstmp-sq',
						'cache.stamps=false\n' +
						'schema.primaryKeyGenerator=sequence\n'
				)
				writeEnv(
						'my56-sq',
						'schema.primaryKeyGenerator=sequence\n'
				)
				writeEnv(
						'my56-sqb',
						'schema.primaryKeyGenerator=batchedSequence\n'
				)
				writeEnv(
						'my56-unique',
						'disableSupport.uniqueViolation=true\n'
				)

				def dockerName = env.JOB_NAME.replace("/", "-") + "-" + env.BUILD_NUMBER
				def dockerDate = new Date().format("yyyyMMdd")
				def mainImage = docker.build(
						'exedio-jenkins:' + dockerName + '-' + dockerDate,
						'--build-arg JDK=' + jdk + ' ' +
						'conf/main')
				def dbImage = docker.build(
						'exedio-jenkins:' + dockerName + '-' + dockerDate + '-db',
						'--build-arg VERSION=' + database + ' ' +
						'--build-arg CONF=my56.cnf ' +
						'conf/mysql')

				def dbBridge = sh ( script:
						"docker network create " +
								dockerName + "-db " +
								"--driver bridge " +
								"--internal",
						returnStdout: true).trim()
				try
				{
					dbImage.withRun(
							"--name '" + dockerName + "-db' " +
							"--tmpfs /var/lib/mysql:rw " +
							"--network " + dbBridge + " " +
							"--dns-opt timeout:1 --dns-opt attempts:1") // fail faster
					{ c ->
						mainImage.inside(
								"--name '" + dockerName + "' " +
								"--hostname mydockerhostname " +
								"--network " + dbBridge + " " +
								"--dns-opt timeout:1 --dns-opt attempts:1") // fail faster
						{
							sh "ant/bin/ant -noinput clean testWithEnv" +
									' "-Dbuild.tag=' + buildTag + '"' +
									' -Dskip.instrument=true' + // already verified in Jenkinsfile
									' -Druntime.test.withEnv.setup.mysql.url=jdbc:mysql://' + dockerName + '-db/' +
									' -Druntime.test.withEnv.setup.mysql.sql=conf/setup-mysql55.sql' +
									' -Ddisable-ansi-colors=true'
						}
						sh "docker logs " + c.id + " &> db.log"
						archiveArtifacts 'db.log'
					}
				}
				finally
				{
					sh "docker network rm " + dbBridge
				}
			}
		}
		catch(Exception e)
		{
			//todo handle script returned exit code 143
			throw e
		}
		finally
		{
			// because junit failure aborts ant
			junit(
					allowEmptyResults: false,
					testResults: 'build/testresults/**/*.xml',
			)
			archiveArtifacts(
					'build/testprotocol.*,' +
					'build/classes/runtime/src/com/exedio/cope/testprotocol.properties,' +
					'build/*.log,' +
					'build/testtmpdir'
			)
			def to = emailextrecipients([culprits(), requestor()])
			//TODO details
			step([$class: 'Mailer',
					recipients: to,
					attachLog: true,
					notifyEveryUnstableBuild: true])

			echo("Delete working dir after build")
			deleteDir()
		}
	}
}

def abortable(Closure body)
{
	try
	{
		body.call()
	}
	catch(hudson.AbortException e)
	{
		if(e.getMessage().contains("exit code 143"))
			return
		throw e
	}
}

def makeBuildTag(scmResult)
{
	return 'build ' +
			env.BRANCH_NAME + ' ' +
			env.BUILD_NUMBER + ' ' +
			new Date().format("yyyy-MM-dd") + ' ' +
			scmResult.GIT_COMMIT + ' ' +
			sh (script: "git cat-file -p " + scmResult.GIT_COMMIT + " | grep '^tree ' | sed -e 's/^tree //'", returnStdout: true).trim()
}

def writeEnv(name, text)
{
	writeFile(
			file: 'conf/environment/' + name + '.properties',
			text:
					'connection.url=jdbc:mysql://' + env.JOB_NAME.replace("/", "-") + "-" + env.BUILD_NUMBER + '-db/test_db_schema\n' +
					'connection.username=test_db_user\n' +
					'connection.password=test_db_password\n' +
					'x-build.schemasavepoint=' +
							/OK: SHOW MASTER STATUS mysql-bin.\\d{6}:\\d+ doDB=binlogtest/ + '\n' +
					'x-build.dialect=mysql\n' +
					'x-build.driver=mysql-connector-java.jar\n' +
					text
		)
}
